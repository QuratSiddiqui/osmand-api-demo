apply plugin: 'com.android.application'

android {
    compileSdkVersion 31
    buildToolsVersion "30.0.3"

    dexOptions {
        jumboMode true
    }

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    lintOptions {
        abortOnError false
    }

    // This is from OsmAndCore_android.aar - for some reason it's not inherited
    aaptOptions {
        // Don't compress any embedded resources
        noCompress "qz", "png"
    }

    sourceSets {
        main {
            manifest.srcFile "AndroidManifest.xml"
            jni.srcDirs = []
            jniLibs.srcDirs = ["libs"]
            java.srcDirs = ["src"]
            renderscript.srcDirs = ["src"]
            resources.srcDirs = ["src"]
            res.srcDirs = ["res"]
            assets.srcDirs = ["assets"]
        }
    }

    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libc++_shared.so'
        pickFirst 'lib/arm64-v8a/libc++_shared.so'
        pickFirst 'lib/x86_64/libc++_shared.so'
        pickFirst 'lib/x86/libc++_shared.so'
    }
    
    flavorDimensions "abi"

    productFlavors {
        x86 {
            dimension "abi"
            ndk {
                abiFilter "x86"
            }
        }
        arm64 {
            dimension "abi"
            ndk {
                abiFilter 'arm64-v8a'
            }
        }
        armv7 {
            dimension "abi"
            ndk {
                abiFilter "armeabi-v7a"
            }
        }
        fat {
            dimension "abi"
            ndk {
                abiFilters 'arm64-v8a', 'x86', 'armeabi-v7a'
            }
        }
    }

    signingConfigs {
        publishing {
            storeFile file("/var/lib/jenkins/osmand_key")
            storePassword System.getenv("OSMAND_APK_PASSWORD")
            keyAlias "osmand"
            keyPassword System.getenv("OSMAND_APK_PASSWORD")
        }
    }

    buildTypes {
        debug {
            signingConfig android.signingConfigs.debug
        }
        nativeDebug {
            signingConfig android.signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.publishing
        }
    }
}

task collectMiscResources(type: Copy) {
    into "src/net/osmand/osm"
    from("../../../resources/poi") {
        include "poi_types.xml"
    }
}

task collectRegionsInfoResources(type: Copy) {
    from "../../../resources/countries-info"
    into "src/net/osmand/map"
    include "regions.ocbf"
}

task collectRoutingResources(type: Sync) {
    from "../../../resources/routing"
    into "src/net/osmand/router"
    include "routing.xml"
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn << [collectMiscResources, collectRegionsInfoResources, collectRoutingResources]
}

dependencies {
    implementation "net.osmand:OsmAnd-java:master-snapshot:android@jar"
    debugImplementation "net.osmand:OsmAnd:master-snapshot:debug@aar"
//    releaseImplementation "net.osmand:OsmAnd:master-snapshot:release@aar"

    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.4.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'
    implementation 'commons-logging:commons-logging-api:1.1'
    implementation 'com.moparisthebest:junidecode:0.1.1'
    implementation 'androidx.preference:preference:1.1.0'
    implementation 'androidx.browser:browser:1.0.0'

    implementation ("com.getkeepsafe.taptargetview:taptargetview:1.13.2"){
        exclude group: 'com.android.support'
    }
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.1'
    implementation ("com.github.HITGIF:TextFieldBoxes:1.4.5"){
        exclude group: 'com.android.support'
    }
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation 'me.zhanghai.android.materialprogressbar:library:1.4.2'
    implementation 'com.jaredrummler:colorpicker:1.1.0'

//    implementation fileTree(include: ['gnu-trove-osmand.jar', 'icu4j-49_1_patched.jar'], dir: 'libs')

    implementation "net.osmand:OsmAndCore_android:master-snapshot@aar"
    debugImplementation "net.osmand:OsmAndCore_androidNativeRelease:master-snapshot@aar"
    nativeDebugImplementation "net.osmand:OsmAndCore_androidNativeDebug:master-snapshot@aar"
    releaseImplementation "net.osmand:OsmAndCore_androidNativeRelease:master-snapshot@aar"

    implementation files('libs/QtAndroid.jar')
    implementation files('libs/QtAndroidBearer.jar')

    // Local build
    //implementation 'com.vividsolutions:jts-core:1.14.0'
    //implementation(name: 'OsmAndCore_android-release', ext: 'aar')
    //implementation(name: 'OsmAndCore_androidNativeRelease-release', ext: 'aar')
}
